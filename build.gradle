buildscript {
    ext.travisBuild = System.getenv("TRAVIS") == "true"
    // allows for -Dpre-dex=false to be set
    ext.preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

    ext.supportVersion = '24.1.0'
    ext.kotlinVersion = '1.1.1'
    ext.ankoVersion = '0.10.0-beta-1'

    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.1'

    defaultConfig {
        applicationId 'com.ferencboldog.ankomaterial'
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName '1.0'
    }

    dexOptions {
        if (travisBuild) {
            incremental false
            preDexLibraries = preDexEnabled && !travisBuild
        } else {
            incremental true
            preDexLibraries = true
        }
    }

//     testOptions {
//         unitTests.returnDefaultValues = true
//     }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    def support = ['appcompat-v7', 'design', 'support-v4', 'recyclerview-v7', 'percent', 'cardview-v7']
    support.each { name ->
        compile "com.android.support:$name:$supportVersion"
    }

    def anko = ['sdk15', 'support-v4', 'design', 'appcompat-v7', 'recyclerview-v7', 'percent', 'cardview-v7']

    anko.each { name ->
        compile "org.jetbrains.anko:anko-$name:$ankoVersion"
    }

    // testCompile 'junit:junit:4.12'
    // testCompile 'org.assertj:assertj-core:2.4.0'
    // testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
}

task wrapper(type: Wrapper) {
    distributionUrl = 'https://services.gradle.org/distributions/gradle-3.4.1-all.zip'
}
